/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	Copyright (C) 2000 Dirk Lausecker -- All Rights Reserved

PROJECT:	BestSound
MODULE:		BestSound Quickplayer
FILE:		uimain.goc

AUTHOR:		Dirk Lausecker


REVISION HISTORY:
	Name	Date		Description
	----	----		-----------
	DL	12.03.2000   	Initial version.
        DL	14.08.2000	Translation for ND

DESCRIPTION:
	see PROCESS.GOC

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

@define	UI_UIMAIN_GOC

@include <bsnwav.goh>		/* dont place behind the other Includes ! */

@include <stdapp.goh>

#include <gstring.h>
#include <Ansi/stdio.h>
#include <Ansi/string.h>
#include <timer.h>
#include <file.h>


@include "UI/uimain.goh"
@include "Main/process.goh"
@include "Main/subcode.goh"
@include "Main/bsqp.goh"

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
 % 		Classdeklaration
 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

@classdecl	BSQPApplicationClass;
@classdecl	BSQPPrimaryClass;

/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
 %		Objects
 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/

/*----------------------------------------------------------------------
 *		AppResource		(ui thread)
 *----------------------------------------------------------------------*/

// Name of helpfile
@ifdef	GERMAN
@define TEXT_HELP_FILE	"bqhelp"
@else
@define TEXT_HELP_FILE	"bsqp"
@endif

@start	AppResource;

@object BSQPApplicationClass BSQPApp = {

    GI_visMoniker = list {@BSQPTextMoniker,@Moniker0,@Moniker1,@Moniker2}
    GI_comp = 	@BSQPPrimary,
                @InfoBox;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = @BSQPPrimary;
    ATTR_GEN_HELP_FILE = @TEXT_HELP_FILE;
}

@visMoniker BSQPTextMoniker = "WAV-QuickPlayer";

@end	AppResource;

/*************************
 *      APPLMONIKER
 *************************/

@start APPLMONIKERRESOURCE, data;
@include <Art/appmkr.goh>
@end APPLMONIKERRESOURCE;

/*----------------------------------------------------------------------
 *		Primary		(ui thread)
 *----------------------------------------------------------------------*/

@start	Interface;

@object GenPrimaryClass BSQPPrimary  =
{
    GI_visMoniker = list {@BSQPTextMoniker,
    			  @Moniker0,
			  @Moniker1,
			  @Moniker2}

    GI_comp = 	@DisplayGroup,
    		@OptionsMenu,
		@TriggerGroup;

    ATTR_GEN_DISPLAY_NOT_MAXIMIZABLE;
    ATTR_GEN_HELP_CONTEXT = "TOC";
    HINT_SIZE_WINDOW_AS_DESIRED;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_PRIMARY_NO_FILE_MENU;
    HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
    HINT_DISPLAY_NOT_RESIZABLE;
/*    HINT_CENTER_WINDOW;	/**/
}

@end Interface;

/*----------------------------------------------------------------------
 *		Display	& Triggergroup	(ui thread)
 *----------------------------------------------------------------------*/

@start	DISPLAYRESOURCE;

/****************
 * DisplayGroup *
 ****************/

@object GenInteractionClass DisplayGroup = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_type = GIT_ORGANIZATIONAL;
    GII_visibility = GIV_SUB_GROUP;
    GI_comp = @DisplayView;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

/***************
 * DisplayView *
 ***************/

@object GenViewClass DisplayView = {
	GVI_horizAttrs = @default | GVDA_NO_LARGER_THAN_CONTENT
			 | GVDA_NO_SMALLER_THAN_CONTENT;
	GVI_vertAttrs =  @default | GVDA_NO_LARGER_THAN_CONTENT
			 | GVDA_NO_SMALLER_THAN_CONTENT;
	GVI_docBounds = {0,0,DISP_VIEW_X_SIZE,DISP_VIEW_Y_SIZE};
	GVI_color = {C_BLUE,0,0,0};
	GVI_content = process;
}

/****************
 * TriggerGroup *
 ****************/

@object GenInteractionClass TriggerGroup =
{
	GI_comp = @STOPTrigger,
        	  @PLAYTrigger,
                  @FileSelectorBox;

    	GII_visibility = GIV_SUB_GROUP;
    	GI_attrs = @default | GA_KBD_SEARCH_PATH;
        HINT_ORIENT_CHILDREN_HORIZONTALLY;
        HINT_CENTER_CHILDREN_VERTICALLY;
        HINT_CENTER_CHILDREN_HORIZONTALLY;
        HINT_EXPAND_WIDTH_TO_FIT_PARENT;
        HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
        HINT_INCLUDE_ENDS_IN_CHILD_SPACING;
        HINT_DRAW_IN_BOX;
}

@object GenTriggerClass STOPTrigger =
{
	GI_visMoniker   = "STOP";
    	GI_attrs = @default | GA_KBD_SEARCH_PATH;
    	GI_kbdAccelerator = ctrl 's';
        GTI_actionMsg   = MSG_PROCESS_BSQP_STOP;
        GTI_destination = process;
}

@object GenTriggerClass PLAYTrigger =
{
	GI_visMoniker   = "PLAY";
    	GI_attrs = @default | GA_KBD_SEARCH_PATH;
    	GI_kbdAccelerator = ctrl 'p';
        GTI_actionMsg   = MSG_PROCESS_BSQP_PLAY;
        GTI_destination = process;
}

@end	DISPLAYRESOURCE;

/*----------------------------------------------------------------------
 *		Infobox
 *----------------------------------------------------------------------*/

@start 	UIMainInfoResource;

@ifdef	GERMAN
@define	TEXT_INFO_1	"Spielt WAV-Dateien nach Doppelklick"

@else
@define	TEXT_INFO_1	"Play WAV files by double-clicking them."
@endif

/***********
 * InfoBox *
 ***********/

@object GenInteractionClass InfoBox = {
    GI_visMoniker = 'I',"Info";
    GI_attrs = @default | GA_KBD_SEARCH_PATH | GIA_NOT_USER_INITIATABLE;
    GI_kbdAccelerator = ctrl 'i';
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_NOTIFICATION;
    GI_comp = 	@InfoGrafik,@InfoGlyph1,@InfoGlyph2,@InfoGlyph3,
    		@InfoGlyph4,@InfoGlyph5,@InfoGlyph6;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    ATTR_GEN_HELP_CONTEXT = "SpecialThanks";
}

@object GenGlyphClass InfoGrafik = {
    GI_visMoniker = list {@BSQPTextMoniker,@Moniker0,@Moniker1,@Moniker2}
}

@object GenGlyphClass InfoGlyph1 = {
    GI_visMoniker = "WAV QuickPlayer";
}

@object GenGlyphClass InfoGlyph2 = {
    GI_visMoniker = @TEXT_INFO_1;
}

@object GenGlyphClass InfoGlyph3 = {
    GI_visMoniker = "\xa9 2000 Dirk Lausecker";
}


@object GenGlyphClass InfoGlyph4 = {
    GI_visMoniker = "Portions of this program are Copyright ";
}

@object GenGlyphClass InfoGlyph5 = {
    GI_visMoniker = "GeoWorks 1993, 1994 and used by permission.";
}

@object GenGlyphClass InfoGlyph6 = {
    GI_visMoniker = "Version X.X - X.X";
}

@end 	UIMainInfoResource;

@start	SELECTORRESOURCE;

/*----------------------------------------------------------------------
 *		File selector
 *----------------------------------------------------------------------*/

@ifdef	GERMAN

@define	TEXT_SELECTOR_1	"W�hle eine WAV-Datei zum Abspielen..."
@define	TEXT_SELECTOR_2	"Ausw�hlen"

@else

@define	TEXT_SELECTOR_1	"Select a file to play..."
@define	TEXT_SELECTOR_2	"Select"

@endif

@object GenInteractionClass FileSelectorBox = {
@ifdef	GERMAN
    GI_visMoniker = '�',"�ffnen";
@else
    GI_visMoniker = 'p',"Open";
@endif

    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    GI_comp = 	@FileSelectorGlyph,
    		@WavFileSelector,
		@FileSelectorOpenTrigger;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    ATTR_GEN_HELP_CONTEXT = "Fileselector";
}

@object GenGlyphClass FileSelectorGlyph = {
    GI_visMoniker = @TEXT_SELECTOR_1;
}

@object GenFileSelectorClass WavFileSelector =
{
        GFSI_destination = process;
    	GFSI_notificationMsg = MSG_BSQP_FILE_SELECTED;
        ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.WAV"};
        ATTR_GEN_PATH_DATA = {SP_DOCUMENT,0};
        HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 15;
}

@object	GenTriggerClass FileSelectorOpenTrigger =
{
	GI_visMoniker = @TEXT_SELECTOR_2;
        GI_states = @default & ~GS_USABLE;
	GTI_actionMsg = MSG_PROCESS_BSQP_OPEN;
        GTI_destination = process;
        HINT_SEEK_REPLY_BAR;
}

@end SELECTORRESOURCE;

@start OPTIONSMENURESOURCE;

/***************
 * OptionsMenu *
 ***************/

@object GenInteractionClass OptionsMenu =
{
    	GII_visibility = GIV_POPUP;
        GI_comp = @OptionsGroup,@TimeDispGroup,@SaveTrigger;
        GI_attrs = @default | GA_KBD_SEARCH_PATH;
        ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_OPTIONS_MENU);
}

/*****************
 * TimeDispGroup *
 *****************/

@object GenInteractionClass TimeDispGroup =
{
	GI_comp = @TimeDispList;
	GII_visibility = GIV_SUB_GROUP;
}

/****************
 * TimeDispList *
 ****************/

@object GenItemGroupClass TimeDispList =
{
	GI_comp = @TimeDispItem1,@TimeDispItem2;
	ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_BSQP_TIME_DISP_LIST_APPLY;
	GIGI_destination = process;
	GIGI_selection = BQ_TIME_DISP_NORMAL;
}

@object GenItemClass TimeDispItem1 =
{
@ifdef GERMAN
	GI_visMoniker = 'S',"Spielzeit";
@else
	GI_visMoniker = 'P',"Play Time";
@endif
	GI_attrs = @default | GA_KBD_SEARCH_PATH;
	GII_identifier = BQ_TIME_DISP_NORMAL;
}

@object GenItemClass TimeDispItem2 =
{
@ifdef GERMAN
	GI_visMoniker = 'R',"Restliche Spielzeit";
@else
	GI_visMoniker = 'R',"Remaining Time";
@endif
	GI_attrs = @default | GA_KBD_SEARCH_PATH;
	GII_identifier = BQ_TIME_DISP_REMAIN;
}

/****************
 * OptionsGroup *
 ****************/
@object GenInteractionClass OptionsGroup = {
	GI_comp = @OptionsBooleans;
	GII_visibility = GIV_SUB_GROUP;
}

/*******************
 * OptionsBooleans *
 *******************/

@object GenBooleanGroupClass OptionsBooleans =
{
	GI_comp = 	@OptionsBoolItem1,
			@OptionsBoolItem2,
			@OptionsBoolItem3;
	GBGI_selectedBooleans = 0;
	GBGI_destination = process;
	GBGI_applyMsg = MSG_BSQP_OPTIONS_APPLY;
}

@object GenBooleanClass OptionsBoolItem1 =
{
@ifdef GERMAN
	GI_visMoniker = 'D',"Dateiauswahlbox nach Doppelklick schlie�en";
@else
	GI_visMoniker = 'C',"Close File Selector on Double-click";

@endif
   	GBI_identifier =BQ_OPTION_CLOSE_SELECTOR;
}

@object GenBooleanClass OptionsBoolItem2 =
{
@ifdef GERMAN
	GI_visMoniker = 'A',"Aussteuerungsanzeige";
@else
	GI_visMoniker = 'L',"Display Volume Level Meter";

@endif
   	GBI_identifier =BQ_OPTION_LEVELMETER;
}

@object GenBooleanClass OptionsBoolItem3 =
{
@ifdef GERMAN
	GI_visMoniker = 'E',"Endloswiedergabe";
@else
	GI_visMoniker = 'M',"Repeat Mode";

@endif
   	GBI_identifier =BQ_OPTION_ENDLESS;
}

/***************
 * SaveTrigger *
 ***************/

@object GenTriggerClass SaveTrigger =
{
@ifdef GERMAN
	GI_visMoniker = 'O',"Optionen speichern";
@else
	GI_visMoniker = 'O',"Save Options";
@endif
	GI_states = @default;
	GI_attrs = @default | GA_KBD_SEARCH_PATH;
	GI_kbdAccelerator = ctrl 's';
	GTI_destination = process;
	GTI_actionMsg = MSG_META_SAVE_OPTIONS;
}



@end OPTIONSMENURESOURCE;


/*%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
 %		Code
 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*/


/**********************************************************

	MSG_META_IACP_NEW_CONNECTION

 	The user has doubleclicked in the NewManager
        System request reaction from the associated geode

 IN:    appLaunchBlock

 **********************************************************/

@method BSQPApplicationClass, MSG_META_IACP_NEW_CONNECTION
{
        AppLaunchBlock	*aplb;
	DiskHandle	dh = 0;
        char		*pB = &fullPath,c;

        // get minimize state
	bqMinimizedState = @call BSQPPrimary::MSG_GEN_DISPLAY_GET_MINIMIZED();
	@callsuper();

        if (appLaunchBlock && (quickMode == BQ_QUICK_OFF))
        {
            aplb = MemLock(appLaunchBlock);
            dh = aplb->ALB_diskHandle;
            dh = FileConstructFullPath(&pB,	// pointer to pointer
            				256, 	// Length of buffer
                                        dh,	// This can be a disk or
                                        	// standard path
                                        aplb->ALB_path,
                                        TRUE);	// Prepend drive name

            strcat(fullPath,"\\");
            strcat(fullPath,aplb->ALB_dataFile);

            // check filename
	    c = aplb->ALB_dataFile[0];
            if (c)
            {
            	// Display WAV-Name
  		@call BSQPPrimary::MSG_GEN_REPLACE_VIS_MONIKER_TEXT(aplb->ALB_dataFile,VUM_NOW);

                // Play File
                BQ_CheckForPlay();
		BQ_StartPlay();
            };
            MemUnlock(appLaunchBlock);
        };
}
