@include "options.goh"
@ifndef COMPILE_OPTION_TURN_OFF_LOGO
@include "glbanim.goh"
@endif

@ifdef COMPILE_OPTION_PARENTAL_CONTROL
@include <parentc.goh>
@endif

@ifdef COMPILE_OPTION_IDIAL_CONTROL
@include <Objects/iDialCC.goh>
@endif

/*
 *              Application Object
 */
@start AppResource;

@visMoniker TextMoniker = "WebMagick";
@include "Art/appicon.goh"

@object HTMLVApplicationClass HTMLVApp = {
         GI_visMoniker = @list{@TextMoniker,@Moniker0,@Moniker1,
                         @Moniker2,@Moniker3,@Moniker4}
    ATTR_GEN_INIT_FILE_CATEGORY = HTMLVIEW_CATEGORY;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    ATTR_GEN_HELP_FILE = "WebMagick Help File";
    /* ATTR_GEN_APPLICATION_SAVE_OPTIONS_TRIGGER = @SaveOptionsButton; */
    ATTR_GEN_APPLICATION_PRINT_CONTROL = @PrintControl;

    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS)=
      @PrintControl,
@ifdef COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON
      @FontControl,
      @SizeControl,
@endif
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
      @ToolControl,
@endif
      @DocumentControl,
      @EditControl,
      @SearchReplaceControl,
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
      @htmlv_ui_obj0050,
@endif
      @ViewControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST)=
      @PrintControl,
@ifdef COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON
      @FontControl,
      @SizeControl,
@endif
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
      @ToolControl,
@endif
      @DocumentControl,
      @EditControl,
      @SearchReplaceControl,
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
      @htmlv_ui_obj0050,
@endif
      @ViewControl;
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
      @ToolControl;
@endif
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
      @DocumentControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) =
      @HTMLVPrimary;

    GI_comp = @HTMLVPrimary;
}
@end AppResource;


/*
 *              File Menu
 */
@start FileResource;
@include "htmlv_ui_men_file.goh"
@end FileResource;


/*
 *              Edit Menu
 */
@start EditResource;
@include "htmlv_ui_men_edit.goh"
@end EditResource;


/*
 *              View Menu
 */
@start ViewResource;
@include "htmlv_ui_men_view.goh"
@end ViewResource;


/*
 *              Options Menu
 */
@start OptionsResource;
@include "htmlv_ui_men_options.goh"
@end OptionsResource;


/*
 *              Navigational Menus
 */
@start NavigateResource;
@include "htmlv_ui_men_nav.goh"
@include "htmlv_ui_men_history.goh"
@include "htmlv_ui_men_kidguard.goh"
@include "htmlv_ui_men_autobrowse.goh"
@include "htmlv_ui_men_js.goh"
@end NavigateResource;


/*
 *              Bookmark Menu
 */
@ifdef COMPILE_OPTION_BOOKMARKS
    @include "bookui.goh"
@endif

/*
 *              Favorites Menu
 */
@ifdef COMPILE_OPTION_FAVORITES // this is where we roll
    @include "favorui.goh"
@endif

/*
 *              Local Menu
 */
@ifdef COMPILE_OPTION_LOCAL_PAGES
@start LocalUIResource;
    @include "localui.goh"
@end LocalUIResource;
@endif

/*
 * HTML Menu
 */
@start HTMLMenuResource;
    @include "htmlv_ui_men_html.goh"
@end HTMLMenuResource;

/*
 * Search Menu
 */
@start SearchResource;
@ifdef COMPILE_OPTION_SEARCH_MENU
    @include "htmlv_ui_men_search.goh"
@endif
@end SearchResource;


/*
 *              Window Menu
 */
@start WindowResource;
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
    @object GenDisplayControlClass htmlv_ui_obj0050 = {
}
@endif
@end WindowResource;

/*
 * Download Dialog
 */
@start DownloadDialogResource;
@include "htmlv_ui_dlg_dwnl.goh"
@object GenInteractionClass DownloadDlg = {
    GI_visMoniker = "Save Web File";
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE
                         | GIA_INITIATED_VIA_USER_DO_DIALOG
                         | GIA_MODAL;
    GI_comp = @DownloadFileSel,@DownloadBtn,@DownloadAbortBtn,@DownloadNameText;
}
@end DownloadDialogResource;

/*
 * Cache Cleanup Dialog
 */
@start CacheCleanupDialogResource;
@object GenInteractionClass CacheCleanupDialog = {
    GI_visMoniker = "Cache Cleanup";
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE
                         | GIA_MODAL;
    GI_comp = @CacheCleanupText;
}

@object GenTextClass CacheCleanupText = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_text = "Cleaning up previous browsing data. Please wait a moment.";
    HINT_FIXED_SIZE = {
        SST_AVG_CHAR_WIDTHS | 40,
        0,
        0
    };
}
@end CacheCleanupDialogResource;

/*
 * Status indicators, to be used in the status bar or elsewhere
 */
@start StatusResource;
    @include "htmlv_ui_status_indctrs.goh"
@end StatusResource;


/*
 * the mighty toolbars and URL entry field
 */
@start ToolbarResource;

@ifdef COMPILE_OPTION_LITTLE_BUTTON_TOOLBAR
    @include "htmlv_ui_toolb_small.goh"
@endif

@ifdef COMPILE_OPTION_BIG_BUTTON_TOOLBAR
    @include "htmlv_ui_toolb_big.goh"
@endif

@object URLEntryClass CurrentURLText = {
@ifndef GLOBAL_INTERNET_BUILD
@ifdef COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR
    GI_visMoniker = 'W',"Web Page:";
@else
    GI_visMoniker = 'U',"URL";
@endif
@endif
@ifdef GLOBAL_INTERNET_BUILD
    GI_attrs = @default | GA_TARGETABLE;
/* allow URL entry to forward search request to HTML area */
/*    ATTR_GEN_TEXT_DO_NOT_INTERACT_WITH_SEARCH_CONTROL;*/
@endif
    GTXI_maxLength = HTML_MAX_BUF-1;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT | GTA_TAIL_ORIENTED;
    GTXI_applyMsg = MSG_HMLVP_URL_CHANGED;
    GTXI_destination = process;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_TEXT_DO_NOT_MAKE_LARGER_ON_PEN_SYSTEMS;
    ATTR_GEN_FOCUS_HELP = @CurrentURLTextHelp ;
    /* to allow user to press return after loading error */
    ATTR_GEN_SEND_APPLY_MSG_ON_APPLY_EVEN_IF_NOT_MODIFIED;
}
@chunk TCHAR CurrentURLTextHelp[] = _TEXT("Enter the desired internet web page address") ;


// the darn go button variant of the toolbar. one day i'm gonna kill it with fire.
@ifdef COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR
@include "htmlv_ui_toolb_simple_fg.goh"
@object GenInteractionClass SimpleURLToolbar = {
    GI_comp = @SimpleToolbarGo, @URLToolbar ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_ALLOW_CHILDREN_TO_WRAP;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CUSTOM_CHILD_SPACING = { 0 };
}
@endif

@object GenInteractionClass URLToolbar = {
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_ALLOW_CHILDREN_TO_WRAP;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_TOOLBOX;
@ifdef COMPILE_OPTION_GOTO_HOTKEY
    GI_visMoniker = 'G',"Goto:";
@endif
    GI_comp = @CurrentURLText;
}

@end ToolbarResource;

/*
* Interface
*/
@start Interface;
@chunk TCHAR HomeHelp[]           = _TEXT("Display home page") ;
@chunk TCHAR BackHelp[]           = _TEXT("Display previous page") ;
@chunk TCHAR BackDisabledHelp[]   = _TEXT("No previous page to display") ;
@chunk TCHAR NextHelp[]           = _TEXT("Display the next page") ;
@chunk TCHAR NextDisabledHelp[]   = _TEXT("A next page does not exist") ;
@chunk TCHAR StopHelp[]           = _TEXT("Stop the current download") ;
@chunk TCHAR ReloadHelp[]         = _TEXT("Update the current page") ;
@chunk TCHAR ReloadDisabledHelp[] = _TEXT("No page is available to update") ;
@chunk TCHAR LoadGHelp[]          = _TEXT("Load graphics for current page") ;
@chunk TCHAR LoadGDisabledHelp[]  = _TEXT("No graphics are available for loading") ;

@ifdef COMPILE_OPTION_HTML_EDITOR
@chunk TCHAR EditHelp[]           = _TEXT("Edit the current page") ;
@chunk TCHAR EditDisabledHelp[]   = _TEXT("No page is available to edit") ;
@endif

/*
 * The HTML Content
 */
@object GenDisplayGroupClass HTMLDisplayGroup = {
    HINT_INITIAL_SIZE = {
      SST_PCT_OF_FIELD_WIDTH | PCT_70,
      SST_PCT_OF_FIELD_HEIGHT | PCT_55,
      0
    };
    HINT_DEFAULT_FOCUS;
    HINT_DEFAULT_TARGET;
    HINT_DISPLAY_GROUP_TILE_VERTICALLY;
}

/*
 * the status bar
 */
@object GenInteractionClass StatusBar = {
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_TOOLBOX;
    GI_comp =
                @LinkTextBox,
@ifndef COMPILE_OPTION_TOGGLE_BARS
@ifdef COMPILE_OPTION_SECURE_STATUS
                @SecureStatus,
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
                @ParentalControlStatus,
@endif
@endif
@ifdef COMPILE_OPTION_SHOW_ALLOC_WATCHER_READ_OUT
                @AllocWatcherReadout,
@endif
                @ProgressIndicatorBox;
}

/*
 * setting up the top bar and the whole UI wrapper
 */
@object GenInteractionClass htmlv_top_bar_left = {
@ifdef COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR
    GI_comp = @SimpleToolbar, @URLToolbar, @SecureStatus ;
    //GI_comp = @SimpleToolbar, @SimpleURLToolbar ;
    //GI_comp = @SimpleToolbar ;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS|0 };
    HINT_CENTER_CHILDREN_VERTICALLY ;
@endif
@ifdef COMPILE_OPTION_LITTLE_BUTTON_TOOLBAR
    GI_comp = @MainToolbar, @URLToolbar ;
@endif
@ifdef COMPILE_OPTION_BIG_BUTTON_TOOLBAR
    GI_comp = @IconBar, @URLToolbar ;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS|0 };
@endif
    HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    //HINT_ORIENT_CHILDREN_VERTICALLY;
}

@object GenInteractionClass htmlv_top_bar = {
@ifdef COMPILE_OPTION_TOGGLE_BARS
    GI_comp = @htmlv_top_bar_left
@ifndef COMPILE_OPTION_TURN_OFF_LOGO
                    ,@SpinningLogo
@endif
                ;
@else
    GI_comp = @htmlv_top_bar_left, @SpinningLogo ;
@endif
    HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
@ifdef COMPILE_OPTION_BIG_BUTTON_TOOLBAR
    HINT_BOTTOM_JUSTIFY_CHILDREN;
@else
    HINT_CENTER_CHILDREN_VERTICALLY ;
@endif
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
    HINT_TOOLBOX;
}

@object GenInteractionClass htmlv_ui_obj0051 = {
    GI_attrs = @default | GA_TARGETABLE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    GI_comp = @htmlv_top_bar, @HTMLDisplayGroup, @StatusBar;
}


@object GenPrimaryClass HTMLVPrimary = {
    /*
     *              Main Window
     */
    ATTR_GEN_HELP_CONTEXT = "TOC";
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    HINT_PRIMARY_NO_FILE_MENU;
    GI_comp = @FileMenu, @EditMenu, @ViewMenu,
@ifdef COMPILE_OPTION_NAVIGATION_MENU
              @NavigateMenu,
@endif
              @OptionsMenu,
@ifdef COMPILE_OPTION_BOOKMARKS
              @BookmarkMenu,
@endif
@ifdef COMPILE_OPTION_FAVORITES
              @FavoritesMenu,
@endif
@ifdef COMPILE_OPTION_HISTORY_ON_MENU_BAR
              @HistoryMenu,
@endif
@ifdef COMPILE_OPTION_LOCAL_PAGES
              @LocalMenu,
@endif
@ifdef COMPILE_OPTION_HTML_MENU
              @HTMLMenu,
@endif
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
              @WindowMenu,
@endif
@ifdef COMPILE_OPTION_SEARCH_MENU
              @SearchMenu,
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
              @PCMenu,
@endif
@ifdef COMPILE_OPTION_AUTO_BROWSE
@ifdef GLOBAL_INTERNET_BUILD
              @AutoBrowseMenu,
@endif
@endif
@if defined(JAVASCRIPT_SUPPORT) && defined(DO_ERROR_CHECKING)
              @JSMenu,
@endif
              @htmlv_ui_obj0051, // this is where the HTML Display Group resides!
@ifdef COMPILE_OPTION_LOCAL_PAGES
              @LocalSaveDlg,
@endif
@ifdef GLOBAL_INTERNET_BUILD
              @JumpDialog,
              @HelpWindow,
@endif
@ifdef COMPILE_OPTION_TOGGLE_BARS
              @GotoDialog,
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
              @PCToggleOffDlg, @PCToggleOnDlg,
              @PCSiteAlreadyAddedDlg, @PCSiteNotInListDlg,
@endif
@ifdef COMPILE_OPTION_IDIAL_CONTROL
              @IDialControl,
@endif
              @CacheCleanupDialog,
              @DownloadDlg/*, @ExpireDialog*/;
    HINT_PRIMARY_FULL_SCREEN ;
    ATTR_GEN_WINDOW_KBD_OVERRIDE = KO_KEYBOARD_REQUIRED;
}

@object GenInteractionClass FileMenu = {
    GI_visMoniker = 'F', "File" ;   /* need moniker now jfh 10/11/02 */
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;         /* create popup menu */
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
//  ATTR_GEN_INTERACTION_GROUP_TYPE = {GIGT_FILE_MENU};
                                        /* don't make file menu jfh - 10/11/02 */
    GI_comp = @DocCtrlHolder, @CustomFileSelDBox,
              @ClearCacheTrigger, @AboutGroup,
              @htmlv_ui_obj0005, @FileExitTrigger;
}

/* I need the DocCtrl so put it here an make it !USABLE.  jfh - 10/11/02 */
@object GenInteractionClass DocCtrlHolder = {
        GI_comp = @DocumentControl;
        GI_states = @default & ~GS_USABLE;
        GII_visibility = GIV_SUB_GROUP;
}
/* here's the dbox replacing GenDoc's file sel.  jfh - 10/11/02 */
@object GenInteractionClass CustomFileSelDBox = {
        GI_visMoniker = "Open";
        GI_comp = @CustomFileSel, @CFSOpen, @CFSCancel;
        GII_visibility = GIV_DIALOG;
}
@object GenFileSelectorClass CustomFileSel = {
        GFSI_fileCriteria = FSFC_DIRS | FSFC_NON_GEOS_FILES;
        GFSI_destination = process;
        GFSI_notificationMsg = MSG_CUSTOM_OPEN_SELECTOR_ACTION;
        ATTR_GEN_PATH_DATA = {SP_DOCUMENT, "."};
        ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.HTM"};
        HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 12;
        HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
}
@object GenTriggerClass CFSOpen = {
        GI_visMoniker = "Open";
        GI_states = @default & ~GS_ENABLED;
        GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
        GTI_destination = process;
        GTI_actionMsg = MSG_CUSTOM_OPEN_HTML_DOC;
        HINT_SEEK_REPLY_BAR;
        HINT_DEFAULT_DEFAULT_ACTION;
}
@object GenTriggerClass CFSCancel = {
        GI_visMoniker = "Cancel";
        GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
        HINT_SEEK_REPLY_BAR;
}

@include "htmlv_ui_dlg_about.goh"
@object GenInteractionClass AboutGroup = {
        GI_comp = @AboutDBox;
        GII_visibility = GIV_SUB_GROUP;
}

@object GenTriggerClass FileExitTrigger = {
    GI_visMoniker = 'x', "Exit WebMagick" ;
    GTI_actionMsg = MSG_META_QUIT ;
    GTI_destination = @HTMLVApp ;
}


@object GenInteractionClass EditMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_EDIT_MENU);
    GI_comp =
        @EditControl,
@ifdef COPY_TO_SCRAPBOOK
        @CopyToScrapbook,
@endif
        @SearchReplaceControl;
}

@object GenInteractionClass ViewMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_VIEW_MENU);
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    GI_comp = @ViewControl,
@ifdef COMPILE_OPTION_TOGGLE_BARS
              @GraphicsGroup,
@else
              @HTMLSettingsGroup,
              @LoadGraphicsGroup,
@endif
              @NavigateReload
@ifdef COMPILE_OPTION_RECALCULATE_BUTTON_ON
            , @RecalculateButton
@endif
@ifdef COMPILE_OPTION_TOGGLE_BARS
            , @ToggleToolbarGroup
@endif
              ;
}

@ifdef COMPILE_OPTION_TOGGLE_BARS
@object GenInteractionClass GraphicsGroup = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_comp = @HTMLSettingsGroup, @LoadGraphicsGroup;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
}

@object GenInteractionClass ToggleToolbarGroup = {
@ifdef COMPILE_OPTION_TOGGLE_GOTO_BAR
@ifdef COMPILE_OPTION_TOGGLE_STATUS_BAR
    GI_comp = @ToggleGotoBar, @ToggleStatusBar;
@else
    GI_comp = @ToggleGotoBar ;
@endif
@else
    GI_comp = @ToggleStatusBar;
@endif
}
@endif

@object GenInteractionClass NavigateMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'N',"Navigate";
    GII_visibility = GIV_POPUP;
    GI_comp = @NavigateHome,@NavigateBack,@NavigateForward,@NavigateStop
@ifndef COMPILE_OPTION_HISTORY_ON_MENU_BAR
              , @HistoryMenu
@endif
@ifdef COMPILE_OPTION_AUTO_BROWSE
@ifndef GLOBAL_INTERNET_BUILD
              ,@htmlv_ui_obj0023,@htmlv_ui_obj0024, @BulkTestWithIndexTrigger
@endif
@endif
              ;
}

@object GenInteractionClass OptionsMenu = {
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_OPTIONS_MENU;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;

@if !defined(COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON) && \
    !defined(COMPILE_OPTION_SHOW_AND_HIDE_TOOLBAR) && \
    !defined(COMPILE_OPTION_ENABLE_SETTINGS) && \
    !defined(COMPILE_OPTION_ENABLE_MISCELLANEOUS) && \
    !defined(COMPILE_OPTION_CUSTOMIZE_TOOL_BARS) && \
     defined(COMPILE_OPTION_AUTO_SAVE_OPTIONS) && \
    !defined(COMPILE_OPTION_ENABLE_NUMCONN_OPTION)
    GI_states = (@default) & ~GS_USABLE;
@endif

    GI_comp =
@ifdef COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON
              @htmlv_ui_obj0026,
@endif
@ifdef COMPILE_OPTION_SHOW_HIDE_TOOLBAR
              @ShowHideToolbarGroup,
@endif
              @htmlv_ui_obj0033,
              @htmlv_ui_obj0041
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
              , @htmlv_ui_obj0046
@endif
@ifdef COMPILE_OPTION_ENABLE_NUMCONN_OPTION
              , @NumConnBooleanGroup
@endif
@ifndef COMPILE_OPTION_AUTO_SAVE_OPTIONS
              , @SaveOptionsGroup
@endif
              ;
}

@ifdef COMPILE_OPTION_BOOKMARKS
@object GenInteractionClass BookmarkMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'B', "Bookmarks" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @BookmarkTriggerAdd,
              @BookmarkTriggerList,
              @BookmarksDialogList,
              @BookmarkEditDialog ;
}
@endif

@ifdef COMPILE_OPTION_FAVORITES
@object GenInteractionClass FavoritesMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'a', "Favorites" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @FavoriteTriggerAdd,
              @FavoriteEditDialog,
              @FavoriteEditGroupDialog,
              @FavoritesDialogList,
              @FavoriteTriggerList,
              @FavoritesMoreTrigger,
              @FavoriteCreateDialog,
              @FavoritesListDialog,
              @FavoriteCreateGroupDialog ;
}
@endif

@ifdef COMPILE_OPTION_LOCAL_PAGES
@object GenInteractionClass LocalMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'L', "Local" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @LocalSaveTrigger,
              @LocalDelete,
              @LocalUpdateTrigger,
              @LocalList;
}
@endif

@ifdef COMPILE_OPTION_HTML_MENU
@object GenInteractionClass HTMLMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'T', "HTML" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @SaveViewHTMLGroup
@ifdef COMPILE_OPTION_HTML_EDITOR
            , @EditTrigger, @DefineHTMLEditor
@endif
              ;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
}
@endif

@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
@object GenInteractionClass WindowMenu = {
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_WINDOW_MENU;
    GI_comp = @htmlv_ui_obj0050;
}
@endif

@ifdef COMPILE_OPTION_SEARCH_MENU
@object GenInteractionClass SearchMenu = {
    GI_visMoniker = 'S', "Search";
    GII_visibility = GIV_POPUP;
    GI_comp = @Search1, @Search2, @Search3, @Search4;
}
@endif

@end Interface;

@ifndef COMPILE_OPTION_TURN_OFF_LOGO
@start Interface;
    @object GlobeAnimClass SpinningLogo = {
        GAI_autoRun = FALSE;
}
@end Interface;
@endif

//@start SimpleToolbarResource ;
//@end SimpleToolbarResource ;

@ifdef GLOBAL_INTERNET_BUILD
@start HelpUIResource;
    @visMoniker HelpWindowMoniker = "Help";
    @object GenInteractionClass HelpWindow = {
        GI_comp = @HelpTop;
        GII_visibility = GIV_DIALOG;
        GII_attrs = @default  | GIA_NOT_USER_INITIATABLE;
        HINT_INTERACTION_MAKE_RESIZABLE;
        HINT_SIZE_WINDOW_AS_RATIO_OF_PARENT = {
            SWSS_RATIO | PCT_50,
            SWSS_RATIO | PCT_80
        };
        HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT = {
            SWSS_RATIO | PCT_50,
            SWSS_RATIO | PCT_15
        };
    }
    @object GenInteractionClass HelpTop = {}
@end HelpUIResource;

@start HelpFrameResource;
    @object URLFrameClass HelpFrame = {}
@end HelpFrameResource;
@endif

@include "Art/toolicon.goh"
@include "icons/icons.goh"

//@start IconBarResource ;
//@include "htmlv_ui_toolb_big.goh"
//@end IconBarResource ;

//@include "init/expireui.goh"


@ifdef COMPILE_OPTION_PARENTAL_CONTROL
@start PCRootResource;
    @object ParentalControlClass PCRoot = {}
@end PCRootResource;

@start PCResource;
    @include "htmlv_ui_dlg_kidguard.goh"
@end PCResource;
@endif

@ifdef COMPILE_OPTION_IDIAL_CONTROL
@start Interface;
    @object IDialControlClass IDialControl = {};
@end Interface;

@endif
