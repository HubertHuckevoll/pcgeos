/**
  * Simple Toolbar
  */

@object GenInteractionClass SimpleToolbar = {
    GI_comp =
        @SimpleToolbarNavigateGroup,
        @URLToolbar,
        @SimpleToolbarViewGroup
@ifdef COMPILE_OPTION_TOGGLE_BARS
@ifdef COMPILE_OPTION_PROGRESS_INDICATOR
      ,  @ProgressIndicatorBox
@endif
      ,  @StatusIndicators
@else
@ifdef GLOBAL_INTERNET_BUILD && 0  /* URL field accepts jumpwords */
      , @SimpleToolbarJump
@else
      , @SimpleToolbarPrint
@endif
@endif
      ;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS | 2 } ;  /* 2 doesn't override toolbox */
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY ;
    HINT_ALLOW_CHILDREN_TO_WRAP ;
    HINT_CENTER_CHILDREN_VERTICALLY;
}

@ifdef COMPILE_OPTION_PROGRESS_INDICATOR
@object GenInteractionClass ProgressIndicatorBox = {
    GI_comp = @ProgressIndicator;
    HINT_FIXED_SIZE = {50|SST_PIXELS, 11|SST_PIXELS, 0};
}
@object FastStatusClass ProgressIndicator = {
    FSI_boxed = TRUE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}
@endif

@ifdef COMPILE_OPTION_TOGGLE_BARS
@object GenInteractionClass StatusIndicators = {
@ifdef TITLE_BAR_STATUS
    GI_states = @default & ~GS_USABLE;
@endif
@if defined(COMPILE_OPTION_SECURE_STATUS) && defined(COMPILE_OPTION_PARENTAL_CONTROL)
    GI_comp = @SecureStatus, @ParentalControlStatus;
@else
@ifdef COMPILE_OPTION_SECURE_STATUS
    GI_comp = @SecureStatus;
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
    GI_comp = @ParentalControlStatus;
@endif
@endif
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
@ifdef TITLE_BAR_STATUS
    HINT_SEEK_TITLE_BAR_RIGHT;  /* dynamically removed for non-TV */
@endif
}
@endif

@object GenInteractionClass SimpleToolbarNavigateGroup = {
    GI_comp =
@ifdef COMPILE_OPTION_TOGGLE_BARS
@ifdef COMPILE_OPTION_TOGGLE_GOTO_BAR
        @SimpleToolbarGoto,
@endif
@endif
        @SimpleToolbarBack,
        @SimpleToolbarNext,
        @SimpleToolbarReload,
        @SimpleToolbarStop,
        @SimpleToolbarHome;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS | 2 } ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
}

@ifdef COMPILE_OPTION_TOGGLE_BARS
@ifdef COMPILE_OPTION_TOGGLE_GOTO_BAR

/* Goto button in toolbar */
@object GenTriggerClass SimpleToolbarGoto = {
    GI_visMoniker = list { @SimpleToolbarGotoMoniker, @GPCGoIconMoniker } ;
    GTI_destination = @GotoDialog;
    GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @SimpleToolbarGotoHelp ;
}
@visMoniker SimpleToolbarGotoMoniker = "Goto" ;
@chunk TCHAR SimpleToolbarGotoHelp[] = _TEXT("Goto a Web Page") ;
@endif

/* Goto dialog for Goto button in toolbar */
@object GenInteractionClass GotoDialog = {
    GI_visMoniker = "Goto";
    GI_comp = @GotoPrompt, @GotoEntry, @GotoGoto, @GotoDont;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default  | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
}
@object GenTextClass GotoPrompt = {
    GTXI_text = "Enter a Web Site to Goto:";
    GI_attrs = @default | GA_READ_ONLY;
}
@object GenTextClass GotoEntry = {
    GTXI_text = "";
    GTXI_maxLength = HTML_MAX_BUF-1;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    HINT_DEFAULT_FOCUS;
    HINT_FIXED_SIZE = { SST_AVG_CHAR_WIDTHS|50, 0 };
}
@object GenTriggerClass GotoGoto = {
    GI_visMoniker = "Goto";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_HMLVP_GOTO;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}
@object GenTriggerClass GotoDont = {
    GI_visMoniker = "Don't Goto";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}
@endif

@object GenTriggerClass SimpleToolbarHome = {
    GI_visMoniker = list { @HomeMoniker, @GPCHomeIconMoniker } ;
    //GI_visMoniker = list { @HomeMoniker0 } ;
    GTI_destination = process ;
    GTI_actionMsg = MSG_HMLVP_HOME_TRIGGER ;
    ATTR_GEN_FOCUS_HELP = @HomeHelp ;
    HINT_NO_BORDERS_ON_MONIKERS;
}

@object GenTriggerClass SimpleToolbarBack = {
    GI_visMoniker = list { @BackMoniker, @GPCBackIconMoniker } ;
    //GI_visMoniker = list { @BackMoniker0};
    GTI_destination = (TO_APP_MODEL) ;
    GTI_actionMsg = MSG_URL_DOCUMENT_BACK ;
    ATTR_GEN_FOCUS_HELP = @BackHelp ;
    ATTR_GEN_FOCUS_DISABLED_HELP = @BackDisabledHelp ;
    HINT_NO_BORDERS_ON_MONIKERS;
}

@object GenTriggerClass SimpleToolbarNext = {
    GI_visMoniker = list { @NextMoniker, @GPCNextIconMoniker } ;
    //GI_visMoniker = list { @FwdMoniker0 } ;
    GTI_destination = (TO_APP_MODEL) ;
    GTI_actionMsg = MSG_URL_DOCUMENT_FORWARD ;
    ATTR_GEN_FOCUS_HELP = @NextHelp ;
    ATTR_GEN_FOCUS_DISABLED_HELP = @NextDisabledHelp ;
    HINT_NO_BORDERS_ON_MONIKERS;
}

@object GenTriggerClass SimpleToolbarReload = {
    GI_visMoniker = list { @ReloadMoniker, @GPCReloadIconMoniker } ;
    //GI_visMoniker = list { @ReloadMoniker0 } ;
    GTI_destination = (TO_APP_MODEL) ;
    GTI_actionMsg = MSG_URL_DOCUMENT_RELOAD ;
    ATTR_GEN_FOCUS_HELP = @ReloadHelp ;
    ATTR_GEN_FOCUS_DISABLED_HELP = @ReloadDisabledHelp ;
    GI_states = @default & ~GS_USABLE;
    HINT_NO_BORDERS_ON_MONIKERS;
}

@object GenTriggerClass SimpleToolbarStop = {
    GI_visMoniker = list { @StopMoniker, @GPCStopIconMoniker } ;
    //GI_visMoniker = list { @StopMoniker0 } ;
    GTI_destination = @HTMLVApp ;
    GTI_actionMsg = MSG_HMLVA_STOP_TRIGGER ;
    ATTR_GEN_FOCUS_HELP = @StopHelp ;
    HINT_NO_BORDERS_ON_MONIKERS;
}

@object GenInteractionClass SimpleToolbarViewGroup = {
    GI_comp =
        @SimpleToolbarZoomOut,
        @SimpleToolbarZoomHundred,
        @SimpleToolbarZoomIn;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS | 0 } ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
}
@object GenTriggerClass SimpleToolbarZoomIn = {
    GI_visMoniker = list { @GPCZoomInIconMoniker } ;
    GTI_destination = @ViewControl ;
    GTI_actionMsg = MSG_GVC_ZOOM_IN ;
    ATTR_GEN_FOCUS_HELP = @ZoomInHelp ;
}
@chunk TCHAR ZoomInHelp[] = _TEXT("Increase the size of the current page") ;

@object GenTriggerClass SimpleToolbarZoomHundred = {
    GI_visMoniker = list { @GPCZoom100IconMoniker } ;
    GTI_destination = @ViewControl ;
    GTI_actionMsg = MSG_GVC_SET_SCALE ;
/*    ATTR_GEN_TRIGGER_ACTION_DATA = MakeWWFixed(100.0) ; */
    ATTR_GEN_TRIGGER_ACTION_TWO_WORDS = { 0, 100 } ;
    ATTR_GEN_FOCUS_HELP = @ZoomHundredHelp ;
}
@chunk TCHAR ZoomHundredHelp[] = _TEXT("Show current page at normal size") ;

@object GenTriggerClass SimpleToolbarZoomOut = {
    GI_visMoniker = list { @GPCZoomOutIconMoniker } ;
    GTI_destination = @ViewControl ;
    GTI_actionMsg = MSG_GVC_ZOOM_OUT ;
    ATTR_GEN_FOCUS_HELP = @ZoomOutHelp;
}
@chunk TCHAR ZoomOutHelp[] = _TEXT("Decrease the size of the current page") ;

@ifdef GLOBAL_INTERNET_BUILD
@object GenTriggerClass SimpleToolbarJump = {
    GI_visMoniker = list { @JumpMoniker, @GPCPrintIconMoniker } ;
    GTI_destination = @JumpDialog;
    GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @JumpHelp ;
}
@chunk TCHAR JumpHelp[] = _TEXT("Jump to page") ;
@object GenInteractionClass JumpDialog = {
    GI_visMoniker = "Jump";
    GI_comp = @JumpEntry, @JumpJump, @JumpDont;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default  | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
}
@object GenTextClass JumpEntry = {
    GI_visMoniker = 'J',"Jump To:";
    GTXI_text = "";
    GTXI_maxLength = HTML_MAX_BUF-1;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    HINT_DEFAULT_FOCUS;
}
@object GenTriggerClass JumpJump = {
    GI_visMoniker = "Jump";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_HMLVP_JUMP;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}
@object GenTriggerClass JumpDont = {
    GI_visMoniker = "Don't Jump";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}
@endif
@ifndef COMPILE_OPTION_TOGGLE_BARS
@object GenTriggerClass SimpleToolbarPrint = {
    GI_visMoniker = list { @PrintMoniker, @GPCPrintIconMoniker } ;
    GTI_destination = @PrintControl ;
    GTI_actionMsg = MSG_PRINT_CONTROL_INITIATE_PRINT ;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @PrintHelp ;
}
@chunk TCHAR PrintHelp[] = _TEXT("Send copy of current page to the printer") ;
@endif

@ifdef GLOBAL_INTERNET_BUILD
    @visMoniker GoMoniker = "Goto:" ;
@else
    @visMoniker GoMoniker = "Go" ;
@endif
    @visMoniker HomeMoniker = "Home" ;
    @visMoniker BackMoniker = "Back" ;
    @visMoniker NextMoniker = "Next" ;
    @visMoniker ReloadMoniker = "Reload" ;
    @visMoniker StopMoniker = "Stop" ;
@ifdef GLOBAL_INTERNET_BUILD
    @visMoniker JumpMoniker = "Jump" ;
@endif
    @visMoniker PrintMoniker = "Print" ;

@ifdef COMPILE_OPTION_TOGGLE_BARS
@object GenGlyphClass SimpleToolbarGo = {
@else
@object GenTriggerClass SimpleToolbarGo = {
@endif
    GI_visMoniker = list {
@ifndef COMPILE_OPTION_GOTO_HOTKEY
        @GoMoniker,
@endif
        @GPCGoIconMoniker } ;
@ifndef COMPILE_OPTION_TOGGLE_BARS
    GTI_actionMsg = MSG_HMLVP_URL_CHANGED;
    GTI_destination = process;
@endif
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @GoHelp ;
}
@chunk TCHAR GoHelp[] = _TEXT("Retrieve the page for the address list in Web Page") ;

